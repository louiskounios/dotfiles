#!/usr/bin/env zsh

export PROJECTS="$HOME/personal/repos"
export DOTFILES="$PROJECTS/dotfiles"

# Avoid trouble with prompt width.
# See https://github.com/robbyrussell/oh-my-zsh/issues/5157#issuecomment-226031519.
if [ "$(uname -s)" = "Darwin" ]; then
	l="en_GB.UTF-8"
	export LC_ALL="$l"
	export LC_CTYPE="$l"
else
	l="en_GB.utf8"
	export LC_ALL="$l"
	export LC_CTYPE="$l"
fi

# Editors
export EDITOR='vim'
export VEDITOR='code'

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the main path file (system/path.zsh)
[ -f "$DOTFILES/system/path.zsh" ] && source "$DOTFILES/system/path.zsh" || true

# load the rest of the path files
setopt EXTENDED_GLOB
# match all path.zsh files that exist in sub-directories of $DOTFILES
# that are not named `system`.
for file in ${(M)config_files:#$DOTFILES/(^system)/path.zsh}; do
	source "$file"
done

# load antibody plugins
source "$HOME/.zsh_plugins.sh"

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
	source "$file"
done

# load every completion before autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
	source "$file"
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r "$HOME/.zcompdump" 2> /dev/null || stat -f '%Sm' -t '%j' "$HOME/.zcompdump" 2> /dev/null)
if [ $(date +'%j') != $updated_at ]; then
	compinit -i
else
	compinit -C -i
fi

unset config_files updated_at

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f "$HOME/.localrc" ] && source "$HOME/.localrc" || true
